Option Compare Database
Option Explicit

'*************************************************************************************
'* 2022/02/25 h.nema
'*  サーバ名：CEWEB
'*  DB名：CeCrm
'*  テーブル名：m_org
'*************************************************************************************

Dim strSQL As String
Dim rs As Recordset
Dim ADORS As ADODB.Recordset

Public Function V_部門取得_部門コード(v部門コード As Long) As ADODB.Recordset

'//-------------------------------------------
'// 部門コード(org_seq)からレコードを取得
'//-------------------------------------------
    Set ADORS = New ADODB.Recordset

    strSQL = "SELECT * FROM m_org"
    strSQL = strSQL & " WHERE org_code = " & v部門コード
    
    If xCE_CON.State = 0 Then
        Call SQLオープンセット_CEWEB
    End If
    
    ADORS.Open strSQL, xCE_CON, adOpenStatic, adLockReadOnly, adCmdText

    Set V_部門取得_部門コード = ADORS
    Set ADORS = Nothing

End Function


Public Sub V_SQL部門更新()

'//----------------------
'//
'//----------------------
    Set ADORS = New ADODB.Recordset

    strSQL = "SELECT * FROM m_org"
    strSQL = strSQL & " WHERE seq IN (10, 16, 20)"
    strSQL = strSQL & " AND update_datetime > '" & Nz(DMax("更新日", "SQL_部門"), "1900/01/01 00:00") & "'"
    
''    Debug.Print strSQL
    
    If xCE_CON.State = 0 Then
        Call SQLオープンセット_CEWEB
    End If
    
    ADORS.Open strSQL, xCE_CON, adOpenStatic, adLockReadOnly, adCmdText
    
    If ADORS.RecordCount > 0 Then
    
        Set rs = OpenDAOTable("SQL_部門")
    
        Do Until ADORS.EOF
        
            rs.Index = "PrimaryKey"
            rs.Seek "=", CStr(ADORS!seq)
        
            If rs.NoMatch Then
                rs.AddNew
            Else
                rs.Edit
            End If
        
            rs!部門コード = ADORS!seq
            rs!部門名 = ADORS!org_name
            rs!更新日 = ADORS!update_datetime
        
            rs.Update
            
            ADORS.MoveNext
        
        Loop
        
    End If
    
    Set ADORS = Nothing
    Set rs = Nothing

End Sub